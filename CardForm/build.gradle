apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'io.codearte.nexus-staging'
apply plugin: "de.marcphilipp.nexus-publish"

version '4.3.1-SNAPSHOT'

android {
    compileSdkVersion 28
    buildToolsVersion '29.0.2'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 46
        versionName version
    }

    testOptions {
        unitTests {
            includeAndroidResources true
        }
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
    }

    resourcePrefix 'bt_'
}

dependencies {
    implementation 'com.google.android.material:material:1.0.0'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:4.3'
    testImplementation 'org.mockito:mockito-core:2.8.9'
}

/* maven release */
task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    classifier = 'javadoc'
    from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives javadocsJar
    archives sourcesJar
}

signing {
    required {
        !version.endsWith("SNAPSHOT") && !version.endsWith("DEVELOPMENT")
    }
    sign publishing.publications
}

def sonatypeUsername = System.env['SONATYPE_USERNAME']
def sonatypePassword = System.env['SONATYPE_PASSWORD']

nexusStaging {
    packageGroup = "com.braintreepayments"
    stagingProfileId = 'd2424106a9f1f'
    username = sonatypeUsername
    password = sonatypePassword
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'com.braintreepayments'
                artifactId = 'card-form'
                version = "${android.defaultConfig.versionName}"

                pom {
                    name = 'card-form'
                    packaging = 'aar'
                    description = 'A ready-made card form layout that can be included in your Android app, making it easy to accept credit and debit cards.'
                    url = 'https://github.com/braintree/android-card-form'

                    scm {
                        url = 'scm:git@github.com:braintree/android-card-form.git'
                        connection = 'scm:git@github.com:braintree/android-card-form.git'
                        developerConnection = 'scm:git@github.com:braintree/android-card-form.git'
                    }

                    developers {
                        developer {
                            id = 'devs'
                            name = 'Braintree Payments'
                        }
                    }

                    licenses {
                        license {
                            name = 'MIT'
                            url = 'http://opensource.org/licenses/MIT'
                            distribution = 'repo'
                        }
                    }
                }
            }
        }
    }
}
